cmake_minimum_required(VERSION 3.0)
list(APPEND CMAKE_MODULE_PATH @PROJECT_DIR@)
enable_language(PANDOC)

if(NOT @PROJECT_NAME@_FIND_COMPONENTS)
    return()
endif()

function(cached_find_program _var _exe)
    if(${_var})
        return()
    endif()

    find_program(${_var} ${_exe} ${ARGN})
    if(${_var}-NOTFOUND)
        message(SEND_ERROR "Could not find ${_exe} executable")
    else()
        message(STATUS "Found ${_exe}: ${${_var}}")
    endif()
endfunction()

foreach(comp ${@PROJECT_NAME@_FIND_COMPONENTS})
    if(comp STREQUAL plantuml)
        cached_find_program(PLANTUML_EXE plantuml DOC "Plantuml executable")
    elseif(comp STREQUAL convert)
        find_package(ImageMagick COMPONENTS convert REQUIRED)
    elseif(comp STREQUAL dot)
        cached_find_program(DOT_EXE dot DOC "Graphviz dot executable")
    else()
        message(SEND_ERROR "Unknown component")
    endif()
endforeach()

include(CMakeParseArguments)

function(convert_image_for)
    cmake_parse_arguments(CONVERT_IMAGE "" "TARGET;FORMAT" "FILES;OPTIONS" ${ARGN})
    if(NOT CONVERT_IMAGE_FORMAT)
        set(CONVERT_IMAGE_FORMAT png)
    endif()
    if(CONVERT_IMAGE_OPTIONS)
        cmake_parse_arguments(CONVERT_OPTIONS "" "" "PLANTUML;CONVERT;DOT" ${CONVERT_IMAGE_OPTIONS})
        list(APPEND CONVERT_OPTIONS_PLANTUML ${CONVERT_OPTIONS_UNPARSED_ARGUMENTS})
        list(APPEND CONVERT_OPTIONS_DOT ${CONVERT_OPTIONS_UNPARSED_ARGUMENTS})
        list(APPEND CONVERT_OPTIONS_CONVERT ${CONVERT_OPTIONS_UNPARSED_ARGUMENTS})
    endif()

    foreach(_file ${CONVERT_IMAGE_FILES})
        get_filename_component(SRC ${_file} ABSOLUTE)
        get_filename_component(SRC_NAME ${SRC} NAME_WE)
        get_filename_component(SRC_EXT ${SRC} EXT)
        set(OUT ${SRC_NAME}.${CONVERT_IMAGE_FORMAT})

        if(SRC_EXT MATCHES uml)
            set(CMDS ${PLANTUML_EXE} ${SRC} -T${CONVERT_IMAGE_FORMAT} ${CONVERT_OPTIONS_PLANTUML} -o ${CMAKE_CURRENT_BINARY_DIR})
        elseif(SRC_EXT MATCHES dot)
            set(CMDS ${DOT_EXE} ${SRC} -T${CONVERT_IMAGE_FORMAT} ${CONVERT_OPTIONS_DOT} -o ${OUT})
        else()
            set(CMDS ${ImageMagick_convert_EXECUTABLE} ${SRC} -t${CONVERT_IMAGE_FORMAT} ${CONVERT_OPTIONS_CONVERT} -o ${OUT})
        endif()

        add_custom_command(OUTPUT ${OUT} COMMAND ${CMDS} DEPENDS ${SRC})
        list(APPEND OUT_FILES ${OUT})
    endforeach()

    add_custom_target(${CONVERT_IMAGE_TARGET}_images DEPENDS ${OUT_FILES})
    add_dependencies(${CONVERT_IMAGE_TARGET} ${CONVERT_IMAGE_TARGET}_images)
endfunction()
